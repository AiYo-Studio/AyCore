plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
}
version = "1.2.2-BETA"
group = "com.aystudio.core"

allprojects {
    apply plugin: 'java'

    repositories {
        maven {
            name = "AyStudio-Repository"
            url = "https://repo.mc9y.com/snapshots"
        }
    }

    dependencies {
        compileOnly("org.projectlombok:lombok:1.18.24")
        annotationProcessor("org.projectlombok:lombok:1.18.24")
    }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

jar {
    from project(':folia').sourceSets.main.output
    from project(':bukkit').sourceSets.main.output
}

subprojects.each { subproject -> evaluationDependsOn(subproject.path) }

tasks.register('allJar', Jar) {
    dependsOn subprojects.tasks.build
    archivesBaseName = 'AyCore'
    subprojects.each { subproject ->
        from subproject.configurations.archives.allArtifacts.files.collect {
            zipTree(it)
        }
    }
}

tasks.jar.enabled = false

artifacts {
    archives allJar
}

processResources {
    setFilteringCharset("UTF-8")
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact("$buildDir/libs/${project.name}-${project.version}.jar")
        }
    }
    repositories {
        maven {
            name = "ayStudioRepository"
            url = "https://repo.mc9y.com/snapshots"
            credentials {
                username = "$ayStudioRepositoryUsername"
                password = "$ayStudioRepositoryPassword"
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}