buildscript {
    repositories {
        maven { url = uri("https://plugins.gradle.org/m2/") }
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url = "https://repo.mc9y.com/snapshots" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'signing'
    id 'maven-publish'
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.0.4-BETA"
group = "com.aystudio.core"
archivesBaseName = "AyCore"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2807"
    runDir = "run"
    mappings = "snapshot_20171003"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'

        expand 'version': project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('excludes', '')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }
    repositories {
        maven {
            name = "ayStudioRepository"
            url = "https://repo.mc9y.com/snapshots"
            credentials {
                username = "$ayStudioRepositoryUsername"
                password = "$ayStudioRepositoryPassword"
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}